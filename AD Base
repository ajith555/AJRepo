Dim objExcelApplication As Excel.Application
Dim objExcelWorkbook As Excel.Workbook
Dim objExcelWorksheet As Excel.Worksheet
Dim objInbox As Outlook.Folder

Sub ExportEmailsNotReplied()
    ' Initialize Outlook objects
    Set objInbox = Application.Session.GetDefaultFolder(olFolderInbox)

    ' Create Excel objects
    Set objExcelApplication = New Excel.Application
    Set objExcelWorkbook = objExcelApplication.Workbooks.Add
    Set objExcelWorksheet = objExcelWorkbook.Worksheets(1)

    ' Set headers in the Excel worksheet
    With objExcelWorksheet
        .Cells(1, 1) = "Subject"
        .Cells(1, 1).Font.Bold = True
        .Cells(1, 2) = "Received"
        .Cells(1, 2).Font.Bold = True
        .Cells(1, 3) = "Sender"
        .Cells(1, 3).Font.Bold = True
        .Cells(1, 4) = "Excerpts"
        .Cells(1, 4).Font.Bold = True
    End With

    ' Make Excel visible
    objExcelApplication.Visible = True
    objExcelWorkbook.Activate

    ' Format Excel worksheet
    With objExcelWorksheet
        .Columns("A:C").AutoFit
        .Rows.RowHeight = 15
        .Columns("D").ColumnWidth = 100
        .Columns("D").WrapText = False
    End With

    ' Process folders and emails
    Call ProcessFolders(objInbox)
End Sub

Sub ProcessFolders(ByVal objCurrentFolder As Outlook.Folder)
    Dim i As Long
    Dim objMail As Outlook.MailItem
    Dim strReplied As String
    Dim nDateDiff As Integer
    Dim nLastRow As Integer

    For i = objCurrentFolder.Items.Count To 1 Step -1
        If objCurrentFolder.Items(i).Class = olMail Then
            Set objMail = objCurrentFolder.Items(i)
            strReplied = objMail.PropertyAccessor.GetProperty("http://schemas.microsoft.com/mapi/proptag/0x10810003")
            
            ' Calculate the time difference in hours (48 hours = 2 days)
            nDateDiff = DateDiff("h", objMail.ReceivedTime, Now)
            
            If (Not (strReplied = 102)) And (Not (strReplied = 103)) And (nDateDiff <= 48) Then
                nLastRow = objExcelWorksheet.Cells(objExcelWorksheet.Rows.Count, "A").End(xlUp).Row + 1
                With objExcelWorksheet
                    .Cells(nLastRow, 1) = objMail.Subject
                    .Cells(nLastRow, 2) = objMail.ReceivedTime
                    .Cells(nLastRow, 3) = objMail.SenderName
                    .Cells(nLastRow, 4) = Left(Trim(objMail.Body), 100) & "..."
                End With
            End If
        End If
    Next

    ' Recursively process subfolders
    If objCurrentFolder.Folders.Count > 0 Then
        For Each objSubfolder In objCurrentFolder.Folders
            Call ProcessFolders(objSubfolder)
        Next
    End If
End Sub
