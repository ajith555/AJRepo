Option Explicit

Dim objExcelApplication As Excel.Application
Dim objExcelWorkbook As Excel.Workbook
Dim objExcelWorksheet As Excel.Worksheet
Dim objinbox As Outlook.Folder

Sub ExportEmailsNotReplied()
    Dim targetEmail As String
    targetEmail = "kushal-ajith.shetty@ubs.com"
    Dim objNamespace As Outlook.Namespace
    Dim objRootFolder As Outlook.Folder
    Dim objABCFolder As Outlook.Folder
    Dim objABCDuplicatesFolder As Outlook.Folder
    Dim objAGEDRepliedFolder As Outlook.Folder
    
    ' Create Excel objects
    Set objExcelApplication = New Excel.Application
    objExcelApplication.Visible = True ' Set Excel application visible before working with columns

    Set objExcelWorkbook = objExcelApplication.Workbooks.Add
    Set objExcelWorksheet = objExcelWorkbook.Worksheets(1)
    
    ' Set fixed row height for all rows in the worksheet
    objExcelWorksheet.Rows.RowHeight = 22

    With objExcelWorksheet
        .Cells(1, 1) = "Subject"
        .Cells(1, 1).Font.Bold = True
        .Cells(1, 2) = "Received"
        .Cells(1, 2).Font.Bold = True
        .Cells(1, 3) = "Sender"
        .Cells(1, 3).Font.Bold = True
        .Cells(1, 4) = "Recipients"
        .Cells(1, 4).Font.Bold = True
        .Cells(1, 5) = "Excerpts"
        .Cells(1, 5).Font.Bold = True
    End With
    
    ' Get the specified email account and its Inbox folder
    Set objNamespace = Outlook.GetNamespace("MAPI")
    Set objRootFolder = objNamespace.Folders(targetEmail)
    Set objinbox = objRootFolder.Folders("Inbox")
    
    ' Check if the "(1) AGED" folder exists or create it if it doesn't
    Dim foundABC As Boolean
    foundABC = False
    Dim objFolder As Outlook.Folder
    For Each objFolder In objinbox.Folders
        If objFolder.Name = "(1) AGED" Then
            foundABC = True
            Set objABCFolder = objFolder
            Exit For
        End If
    Next objFolder
    
    If Not foundABC Then
        Set objABCFolder = objinbox.Folders.Add("(1) AGED", olFolderInbox)
    End If

    ' Check if the "(1) AGED Duplicates" folder exists or create it if it doesn't
    Dim foundABCDuplicates As Boolean
    foundABCDuplicates = False
    For Each objFolder In objinbox.Folders
        If objFolder.Name = "(1) AGED Duplicates" Then
            foundABCDuplicates = True
            Set objABCDuplicatesFolder = objFolder
            Exit For
        End If
    Next objFolder
    
    If Not foundABCDuplicates Then
        Set objABCDuplicatesFolder = objinbox.Folders.Add("(1) AGED Duplicates", olFolderInbox)
    End If
    
    ' Check if the "(1) AGED Replied" folder exists or create it if it doesn't
    Dim foundAGEDReplied As Boolean
    foundAGEDReplied = False
    For Each objFolder In objinbox.Folders
        If objFolder.Name = "(1) AGED Replied" Then
            foundAGEDReplied = True
            Set objAGEDRepliedFolder = objFolder
            Exit For
        End If
    Next objFolder
    
    If Not foundAGEDReplied Then
        Set objAGEDRepliedFolder = objinbox.Folders.Add("(1) AGED Replied", olFolderInbox)
    End If
    
    ' Check and process the folders
    ProcessFolders objinbox, objABCFolder, objABCDuplicatesFolder, objAGEDRepliedFolder

    ' Format the Excel columns and rows
    With objExcelWorksheet
        .Columns("A:D").AutoFit
        .Columns("E").ColumnWidth = 100
        .Columns("E").WrapText = False
    End With

    RemoveDuplicatesFromWorkbook

    MsgBox "Complete!", vbExclamation
End Sub

Sub ProcessFolders(ByVal objCurrentfolder As Outlook.Folder, ByVal objDestinationFolder As Outlook.Folder, ByVal objDuplicatesFolder As Outlook.Folder, ByVal objRepliedFolder As Outlook.Folder)
    Dim i As Long
    Dim objMail As Outlook.MailItem
    Dim strReplied As String
    Dim nDateDiff As Integer
    Dim nReplyDateDiff As Integer
    Dim nLastRow As Integer
    Dim Recipient As Outlook.Recipient
    Dim mailSubject As String
    Dim mailReceivedTime As Date
    
    On Error Resume Next
    
    For i = objCurrentfolder.Items.Count To 1 Step -1
        If objCurrentfolder.Items(i).Class = olMail Then
            Set objMail = objCurrentfolder.Items(i)
            strReplied = objMail.PropertyAccessor.GetProperty("http://schemas.microsoft.com/mapi/proptag/0x10810003")
            
            ' Add the condition to check if the sender name is to be excluded
            Dim excludedSenders As String
            excludedSenders = "Shetty, Kushal-Ajith; Sakaria, Pramod" ' Add other sender names as needed
            If InStr(1, excludedSenders, objMail.SenderName, vbTextCompare) = 0 Then
                If (Not (strReplied = "102")) And (Not (strReplied = "103")) Then
                    nDateDiff = DateDiff("d", objMail.SentOn, Now)
                    nReplyDateDiff = DateDiff("d", objMail.ReceivedTime, Now)
                
                    ' Check if email is from the last 7 days and not replied for more than 2 days
                    If nDateDiff <= 7 And nReplyDateDiff > 2 Then
                        mailSubject = objMail.Subject
                        mailReceivedTime = objMail.ReceivedTime
                        
                        ' Check if there are duplicates with the same subject
                        Dim j As Long
                        Dim duplicateCount As Integer
                        duplicateCount = 0
                        For j = objCurrentfolder.Items.Count To 1 Step -1
                            If objCurrentfolder.Items(j).Class = olMail Then
                                If objCurrentfolder.Items(j).Subject = mailSubject And objCurrentfolder.Items(j).ReceivedTime > mailReceivedTime Then
                                    duplicateCount = duplicateCount + 1
                                    If duplicateCount > 1 Then
                                        ' Move the duplicate emails to the "(1) AGED Duplicates" folder
                                        objCurrentfolder.Items(j).Move objDuplicatesFolder
                                    End If
                                End If
                            End If
                        Next j
                        
                        If duplicateCount = 0 Then
                            nLastRow = objExcelWorksheet.Range("A" & objExcelWorksheet.Rows.Count).End(xlUp).Row + 1
                            
                            With objExcelWorksheet
                                .Cells(nLastRow, 1) = objMail.Subject
                                .Cells(nLastRow, 2) = objMail.ReceivedTime
                                .Cells(nLastRow, 3) = objMail.SenderName
                            End With
                        
                            Dim recipients As String
                            recipients = ""
                            For Each Recipient In objMail.Recipients
                                recipients = recipients & Recipient.Name & "; "
                            Next Recipient
                            recipients = Left(recipients, Len(recipients) - 2) ' Remove trailing "; "
                        
                            With objExcelWorksheet
                                .Cells(nLastRow, 4) = recipients ' Add recipients' names to the new column
                                .Cells(nLastRow, 5) = Left(Trim(objMail.Body), 100) & "..." ' Excerpts
                            End With

                            ' Move the retrieved email to the "(1) AGED" folder
                            If objCurrentfolder.Name <> objDestinationFolder.Name Then
                                objMail.Move objDestinationFolder
                            End If
                        End If
                    End If
                End If
            End If
        End If
    Next i

    On Error GoTo 0
    
    If objCurrentfolder.Folders.Count > 0 Then
        Dim objSubfolder As Outlook.Folder
        For Each objSubfolder In objCurrentfolder.Folders
            ProcessFolders objSubfolder, objDestinationFolder, objDuplicatesFolder, objRepliedFolder
        Next objSubfolder
    End If
End Sub

Sub RemoveDuplicatesFromWorkbook()
    Dim lastRow As Long
    Dim lastCol As Long
    Dim ws As Worksheet
    Dim rng As Range
    
    Set ws = objExcelWorkbook.Worksheets(1)
    lastRow = ws.Cells(ws.Rows.Count, 1).End(xlUp).Row
    lastCol = ws.Cells(1, ws.Columns.Count).End(xlToLeft).Column
    Set rng = ws.Range(ws.Cells(1, 1), ws.Cells(lastRow, lastCol))
    rng.RemoveDuplicates Columns:=Array(1, 2, 3, 4, 5), Header:=xlYes
End Sub
