Option Explicit ' Add this line at the beginning of the module

Dim objExcelApplication As Excel.Application
Dim objExcelWorkbook As Excel.Workbook
Dim objExcelWorksheet As Excel.Worksheet

Sub ExportEmailsNotReplied()
    Dim objInbox As Outlook.Folder

    Set objExcelApplication = CreateObject("Excel.Application")
    Set objExcelWorkbook = objExcelApplication.Workbooks.Add
    Set objExcelWorksheet = objExcelWorkbook.Worksheets(1)
    
    With objExcelWorksheet
        .Cells(1, 1).Value = "Subject"
        .Cells(1, 1).Font.Bold = True
        .Cells(1, 2).Value = "Received"
        .Cells(1, 2).Font.Bold = True
        .Cells(1, 3).Value = "Sender"
        .Cells(1, 3).Font.Bold = True
        .Cells(1, 4).Value = "Excerpts"
        .Cells(1, 4).Font.Bold = True
    End With
    
    objExcelApplication.Visible = True
    objExcelWorkbook.Activate
    
    Set objInbox = Application.Session.GetDefaultFolder(olFolderInbox)
    Call ProcessFolders(objInbox)
    
    With objExcelWorksheet
        .Columns("A:C").AutoFit
        .Rows.RowHeight = 15
        .Columns("D").ColumnWidth = 100
        .Columns("D").WrapText = False
    End With
    
    MsgBox "Complete!", vbExclamation
End Sub

Sub ProcessFolders(ByVal objCurrentFolder As Outlook.Folder)
    Dim i As Long
    Dim objMail As Outlook.MailItem
    Dim strReplied As String
    Dim nDateDiff As Integer
    Dim nLastRow As Integer
    
    For i = objCurrentFolder.Items.Count To 1 Step -1
        If objCurrentFolder.Items(i).Class = olMail Then
            Set objMail = objCurrentFolder.Items(i)
            strReplied = objMail.PropertyAccessor.GetProperty("http://schemas.microsoft.com/mapi/proptag/0x10810003")
            
            If (Not (strReplied = "102")) And (Not (strReplied = "103")) Then
                nDateDiff = DateDiff("d", objMail.SentOn, Now)
                
                If nDateDiff < 7 Then
                    nLastRow = objExcelWorksheet.Range("A" & objExcelWorksheet.Rows.Count).End(xlUp).Row + 1
                    
                    With objExcelWorksheet
                        .Cells(nLastRow, 1).Value = objMail.Subject
                        .Cells(nLastRow, 2).Value = objMail.ReceivedTime
                        .Cells(nLastRow, 3).Value = objMail.SenderName
                        .Cells(nLastRow, 4).Value = Left(Trim(objMail.Body), 100) & "..."
                    End With
                End If
            End If
        End If
    Next
    
    If objCurrentFolder.Folders.Count > 0 Then
        Dim objSubfolder As Outlook.Folder
        For Each objSubfolder In objCurrentFolder.Folders
            Call ProcessFolders(objSubfolder)
        Next
    End If
End Sub
