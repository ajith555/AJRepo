Option Explicit

Dim objExcelApplication As Excel.Application
Dim objExcelWorkbook As Excel.Workbook
Dim objExcelWorksheet As Excel.Worksheet
Dim objinbox As Outlook.Folder

Sub ExportEmailsNotReplied()
    Dim targetEmail As String
    targetEmail = "testing@ubs.com"
    Dim objNamespace As Outlook.Namespace
    Dim objRootFolder As Outlook.Folder
    Dim objABCFolder As Outlook.Folder
    
    Set objExcelApplication = CreateObject("Excel.Application")
    Set objExcelWorkbook = objExcelApplication.Workbooks.Add
    Set objExcelWorksheet = objExcelWorkbook.Worksheets(1)
    
    With objExcelWorksheet
        .Cells(1, 1) = "Subject"
        .Cells(1, 1).Font.Bold = True
        .Cells(1, 2) = "Received"
        .Cells(1, 2).Font.Bold = True
        .Cells(1, 3) = "Sender"
        .Cells(1, 3).Font.Bold = True
        .Cells(1, 4) = "Recipients"
        .Cells(1, 4).Font.Bold = True
        .Cells(1, 5) = "Excerpts"
        .Cells(1, 5).Font.Bold = True
    End With
    
    objExcelApplication.Visible = True
    
    ' Get the specified email account and its Inbox folder
    Set objNamespace = Outlook.GetNamespace("MAPI")
    Set objRootFolder = objNamespace.Folders(targetEmail)
    Set objinbox = objRootFolder.Folders("Inbox")
    
    ' Check if the "ABC" folder exists or create it if it doesn't
    Dim foundABC As Boolean
    foundABC = False
    Dim objFolder As Outlook.Folder
    For Each objFolder In objinbox.Folders
        If objFolder.Name = "(1) AGED" Then
            foundABC = True
            Set objABCFolder = objFolder
            Exit For
        End If
    Next objFolder
    
    If Not foundABC Then
        Set objABCFolder = objinbox.Folders.Add("(1) AGED", olFolderInbox)
    End If
    
    ' Check and process the folders
    ProcessFolders objinbox, objABCFolder
    
    ' Format the Excel columns and rows
    With objExcelWorksheet
        .Columns("A:E").AutoFit
        .Columns("D").WrapText = False
        .Columns("E").WrapText = False
    End With
    
    RemoveDuplicatesFromWorkbook
    
    MsgBox "Complete!", vbExclamation
End Sub

Sub ProcessFolders(ByVal objCurrentfolder As Outlook.Folder, ByVal objDestinationFolder As Outlook.Folder)
    Dim i As Long
    Dim objMail As Outlook.MailItem
    Dim strReplied As String
    Dim nDateDiff As Integer
    Dim nReplyDateDiff As Integer
    Dim nLastRow As Integer
    
    For i = objCurrentfolder.Items.Count To 1 Step -1
        If objCurrentfolder.Items(i).Class = olMail Then
            Set objMail = objCurrentfolder.Items(i)
            strReplied = objMail.propertyAccessor.GetProperty("http://schemas.microsoft.com/mapi/proptag/0x10810003")
            
            If (Not (strReplied = "102")) And (Not (strReplied = "103")) Then
                nDateDiff = DateDiff("d", objMail.SentOn, Now)
                nReplyDateDiff = DateDiff("d", objMail.ReceivedTime, Now)
                
                ' Check if email is from the last 7 days and not replied for more than 2 days
                If nDateDiff <= 7 And nReplyDateDiff > 2 Then
                    nLastRow = objExcelWorksheet.Range("A" & objExcelWorksheet.Rows.Count).End(xlUp).Row + 1
                    
                    Dim recipients As String
                    recipients = ExtractEmailAddressFromRecipients(objMail)
                    
                    With objExcelWorksheet
                        .Cells(nLastRow, 1) = objMail.Subject
                        .Cells(nLastRow, 2) = objMail.ReceivedTime
                        .Cells(nLastRow, 3) = objMail.SenderName
                        .Cells(nLastRow, 4) = Left(Trim(recipients), 100) & "..."
                        .Cells(nLastRow, 5) = Left(Trim(objMail.Body), 100) & "..."
                    End With
                    
                    ' Move the retrieved email to the "(1) AGED" folder
                    If objCurrentfolder.Name <> objDestinationFolder.Name Then
                        objMail.Move objDestinationFolder
                    End If
                End If
            End If
        End If
    Next i
    
    If objCurrentfolder.Folders.Count > 0 Then
        Dim objSubfolder As Outlook.Folder
        For Each objSubfolder In objCurrentfolder.Folders
            ProcessFolders objSubfolder, objDestinationFolder
        Next objSubfolder
    End If
End Sub

Sub RemoveDuplicatesFromWorkbook()
    Dim lastRow As Long
    Dim lastCol As Long
    Dim ws As Worksheet
    Dim rng As Range
    
    Set ws = objExcelWorkbook.Worksheets(1)
    lastRow = ws.Cells(ws.Rows.Count, 1).End(xlUp).Row
    lastCol = ws.Cells(1, ws.Columns.Count).End(xlToLeft).Column
    Set rng = ws.Range(ws.Cells(1, 1), ws.Cells(lastRow, lastCol))
    rng.RemoveDuplicates Columns:=Array(1, 2, 3, 4, 5), Header:=xlYes
End Sub

Function ExtractEmailAddressFromRecipients(ByRef objMail As Outlook.MailItem) As String
    Dim recipient As Outlook.Recipient
    Dim recipientsEmails As String
    
    For Each recipient In objMail.Recipients
        recipientsEmails = recipientsEmails & recipient.Address & "; "
    Next recipient
    
    ExtractEmailAddressFromRecipients = recipientsEmails
End Function
